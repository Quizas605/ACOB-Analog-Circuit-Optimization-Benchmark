* Disclaimer:
* No rights or licenses are granted by ams to Recipient, expressly or by implication, with respect to any proprietary information 
* or patent, copyright mask, work, trademark, trade secret or other intellectual property right owned or controlled by ams, except
* as expressly provided by ams. Recipient may not remove any copyright notices from the Simulation File. ams may make changes to 
* the Simulation Files or to items referenced therein at any time without notice and is not obligated to support or update these Files.
* This simulation File is provided by ams "AS IS" and any express or implied warranties, including, but not limited to the implied 
* warranties of merchantability and fitness for a particular purpose are disclaimed. In no event shall ams or its suppliers be liable 
* for any direct, indirect, incidental, special, exemplary or consequential damages (including, but not limited to procurement of 
* substitute goods or services, loss of use, data or profits, or business interruption however caused and on any theory of liability, 
* whether in contract, strict liability or tort (including negligence or otherwise) arising in any way out of the use of - or any 
* inability to use - this Simulation File, even if advised of the possibility of such damage.  
* Â© ams AG

* Nominal supply: 3.3 volts
* Absolute maximum: 3.6 volts
* Modeled temperature range: -40 ~ +125C
* Minimum L,W : 350 nm
* Format      : HSPICE
* These parameters are realistic but do not correspond to any actual process

* Default values for the following are provided for all logic gates
* gt_l  - gate length (all logic gates assigned minimum length, 350 nm, by default)
* gt_nw - 2um  (W/L=~6), n-channel gate width for 1X inverter
* gt_pw - 6um (W/L=~18), p-channel gate width (= 3 * gt_nw) for 1X inverter with VDD/2 switching point
* sx    - 1um when gates share diffusions (default); should use 2um for gates with single transistors (eg inverter) 
.param gt_l=350e-9 gt_nw=2e-6 gt_pw=6e-6 sx=1e-6


.OPTION             COMPAT
+    ABSV=1e-6      CHGTOL=1e-14        DEFAD=5.29e-12      DEFAS=5.29e-12
+    DEFNRD=0.1     DEFNRS=0.1          DEFPD=9.2e-06       DEFPS=9.2e-06
* Parameters for TYPICAL process (typical gain transistors, typical resistances and capacitances, typical logic delay, typical power)

* NMOS TRANSISTOR
* TYPICAL MOS model   : BSIM3v3

.MODEL          NMOS1           NMOS            LEVEL=49  
+MOBMOD=1       CAPMOD=2        NLEV=0          NOIMOD=3        VERSION=3.24  
+K1=0.5         K2=0.034        K3=-1.1         K3B=-0.44       NCH=2.6E+17  
+VTH0=0.5       VOFF=-0.089     DVT0=50         DVT1=1          DVT2=-8.4E-03  
+KETA=0.02      PSCBE1=1.0E+30  PSCBE2=1.0E-06  DVT0W=0.11      DVT1W=6.6E+04  
+DVT2W=-0.014   UA=4.7E-12      UB=2.2E-18      UC=1.0E-20      U0=480  
+DSUB=0.5       ETA0=0.015      ETAB=-0.12      NFACTOR=0.42    EM=4.1E+07  
+PCLM=0.7       PDIBLC1=0.35    PDIBLC2=2.1E-03 DROUT=0.5       A0=2.6  
+A1=0           A2=1            PVAG=0          VSAT=1.4E+05    AGS=0.24       
+B0=4.4E-09     B1=0            DELTA=0.015     PDIBLCB=0.32    W0=2.7E-07  
+DLC=3.0E-08    DWC=9.5E-08     DWB=0           DWG=0           LL=0  
+LW=0           LWL=0           LLN=1           LWN=1           WL=0  
+WW=-1.2E-14    WWL=-9.5E-21    WLN=1           WWN=1           TNOM=27  
+AT=3.3E+04     UTE=-1.8        KT1=-0.34       KT2=0.022       KT1L=0  
+UA1=0          UB1=0           UC1=0           PRT=0           CGSO=1.2E-10  
+CGDO=1.2E-10   CGBO=1.1E-10    CGDL=1.4E-10    CGSL=1.3E-10    CKAPPA=0.6  
+CF=0           ELM=5           XPART=1         CLC=1.0E-15     CLE=0.6  
+RDSW=350       CDSC=0          CDSCB=1.5E-03   CDSCD=1.0E-03   PRWB=-0.25  
+PRWG=0         CIT=4.5E-04     TOX=7.5E-09     NGATE=0         NLX=1.8E-07  
+XL=0           XW=0            ALPHA0=2.6E-06  ALPHA1=5        BETA0=21  
+AF=1.5         KF=2.2E-26      EF=1            NOIA=1.2E+19    NOIB=5.3E+04  
+NOIC=-5.9E-13  ACM=2           RD=0            RS=0            RSH=70  
+RDC=0          RSC=0           LINT=-5.0E-08   WINT=9.5E-08    LDIF=0  
+HDIF=8.0E-07   WMLT=1          LMLT=1          XJ=3.0E-07      JS=5.1E-07  
+JSW=6.0E-13    IS=0            N=1             NDS=1000        VNDS=-1  
+CBD=0          CBS=0           CJ=8.4E-04      CJSW=2.5E-10    FC=0  
+MJ=0.34        MJSW=0.23       XTI=2           TT=0            PB=0.69  
+PHP=0.69      


* PMOS TRANSISTOR
* TYPICAL MOS model   : BSIM3v3

.MODEL          PMOS1           PMOS            LEVEL=49  
+MOBMOD=1       CAPMOD=2        NLEV=0          NOIMOD=3        VERSION=3.24  
+K1=0.59        K2=-0.061       K3=11           K3B=-0.76       NCH=9.2E+16  
+VTH0=-0.7      VOFF=-0.11      DVT0=1.7        DVT1=0.38       DVT2=0.017  
+KETA=-0.014    PSCBE1=1.0E+30  PSCBE2=1.0E-06  DVT0W=0.19      DVT1W=7.3E+04    
+DVT2W=-6.4E-03 UA=5.3E-10      UB=1.1E-18      UC=1.0E-20      U0=150    
+DSUB=0.5       ETA0=0.25       ETAB=-3.9E-03   NFACTOR=1.3     EM=4.1E+07    
+PCLM=3.2       PDIBLC1=1.0E-04 PDIBLC2=1.0E-20 DROUT=0.5       A0=0.59    
+A1=0           A2=1            PVAG=0          VSAT=1.2E+05    AGS=0.24    
+B0=8.9E-08     B1=0            DELTA=0.01      PDIBLCB=1       W0=1.0E-10    
+DLC=2.4E-08    DWC=3.5E-08     DWB=0           DWG=0           LL=0    
+LW=0           LWL=0           LLN=1           LWN=1           WL=0    
+WW=1.8E-16     WWL=-2.0E-21    WLN=1           WWN=1.1         TNOM=27    
+AT=3.3E+04     UTE=-1.3        KT1=-0.55       KT2=0.022       KT1L=0    
+UA1=0          UB1=0           UC1=0           PRT=0           CGSO=8.6E-11    
+CGDO=8.6E-11   CGBO=1.1E-10    CGDL=1.1E-10    CGSL=1.0E-10    CKAPPA=0.6    
+CF=0           ELM=5           XPART=1         CLC=1.0E-15     CLE=0.6    
+RDSW=1.1E+03   CDSC=2.5E-03    CDSCB=3.0E-04   CDSCD=4.3E-04   PRWB=-0.098    
+PRWG=0.14      CIT=0           TOX=7.7E-09     NGATE=0         NLX=1.7E-07    
+XL=0           XW=0            ALPHA0=1.0E-09  ALPHA1=1.5      BETA0=33    
+AF=1.4         KF=1.2E-26      EF=1            NOIA=5.3E+17    NOIB=4.8E+03    
+NOIC=8.1E-13   ACM=2           RD=0            RS=0            RSH=130    
+RDC=0          RSC=0           LINT=-7.1E-08   WINT=3.5E-08    LDIF=0    
+HDIF=8.0E-07   WMLT=1          LMLT=1          XJ=3.0E-07      JS=2.8E-07    
+JSW=3.7E-13    IS=0            N=1             NDS=1000        VNDS=-1    
+CBD=0          CBS=0           CJ=1.4E-03      CJSW=3.5E-10    FC=0    
+MJ=0.54        MJSW=0.46       XTI=1.9         TT=0            PB=1    
+PHP=1.1            


* BIPOLAR TRANSISTOR (VERTICAL)

* TYPICAL SPEED, TYPICAL BETA    
.MODEL          PNP1           PNP    
+IS=2.3E-17     IRB=4.4E-06    IKF=1.3E-03      BF=6            NF=0.99         
+ISE=6.6E-16    NE=1.7         VAF=200          IKR=1.9E-04     BR=0.099    
+NR=0.99        ISC=2.9E-14    NC=1.1           VAR=11          RBM=1    
+RB=220         RE=9.7         AF=1.3           RC=45           KF=9.1E-15    
+TF=6.4E-10     EG=1.2         XTI=5.5          XTB=2.3         CJE=1.4E-13    
+VJE=1.1        MJE=0.54       CJC=4.4E-14      VJC=0.53        MJC=0.32    


* POLYSILICON RESISTOR and CAPACITOR, TYPICAL
* W,L,PERI = width, length, perimeter in metres; AREA = area in metres^2

.SUBCKT PRIMITIVE_RPOLY2 N1 N2 W=1e-6 L=1e-6
R1 N1 N2 RPOLY2_MODEL R='50*L/(W-(2.5e-07))*(1+2.2e-06*(5.8e-04)*W**(0.5)*(V(N1,N2)/L)**2)' TC1=5.8e-04 TC2=7.5e-07 
* For L=W:  66 ohms @ 0V; 151 ohms @ 1V; 407 ohms @ 2V;  832 ohms @3V
.MODEL RPOLY2_MODEL R NOISE=1 KF='5e-23*(W-2.5e-07)/(L*(W-2.5e-07)**2)' AF=2 EF=1 L=L W=W
.ENDS

.SUBCKT PRIMITIVE_CPOLY N1 N2 AREA=0 PERI='4*AREA**0.5'
C1 N1 N2 C='(0.86e-03*AREA+0.086e-09*PERI)*(1+85e-6*V(N1,N2))' TC1=3e-5 CTYPE=1
* <0.1% voltage dependence; for PERI=sqrt(AREA)*4:  1.2fF @1um^2; 9.7fF @10um^2; 89.5fF @100um^2; 871fF @1000um^2
.ends


* Diodes

* typical       N+ in P substrate
.MODEL          DNINSUB        D                LEVEL=1
+IS=5.0e-07     JSW=0.6e-12    N=1.0            CJ=8.4e-04      M=3.4e-01 
+VJ=6.9e-01     TT=0.0         CJSW=2.5e-10     MJSW=2.3e-01    FC=0.5
+EG=1.1         XTI=2.0        AF=1.0           KF=0.0

* typical       P+ in N well
.MODEL          DPINSUB        D                LEVEL=1
+IS=2.8e-07     JSW=0.4e-12    N=1.0            CJ=1.4e-03      M=5.4e-01 
+VJ=1.0e+00     TT=0.0         CJSW=3.5e-10     MJSW=4.6e-01    FC=0.5
+EG=1.1         XTI=1.9        AF=1.0           KF=0.0

* typical       N well to P substrate
.MODEL          DNWINSUB       D                LEVEL=1
+IS=2.8e-06     JSW=7.6e-12    N=1.0            CJ=8.0e-05      M=3.9e-01 
+VJ=5.3e-01     TT=0.0         CJSW=5.1e-10     MJSW=2.7e-01    FC=0.5
+EG=1.1         XTI=1.5        AF=1.0           KF=0.0


* typical       Zener, forward biased
.MODEL          DZF1           D                LEVEL=1
+IS=1.2e-13                    N=1.1                            M=5.2e-01 
+VJ=1.0         TT=8.0e-08                                      FC=0.5 
+EG=1.1         XTI=0.2 

* typical       Zener, reverse biased
.MODEL          DZR1           D                LEVEL=1
+IS=0.3e-09                    N=6.9
+EG=0.0         XTI=150.0

* typical       substrate diode with varacter model
.MODEL          DWELL1         D                LEVEL=1
+IS=2.8e-06     ISW=0.0        N=1.0                            M=4.7e-01
+VJ=5.3e-01     TT=0.0         CJO=5.5e-05      MJSW=0.0        FC=0.0      
+EG=1.1         XTI=1.5        AF=1.0           KF=0.0   

* typical       substrate diode with varacter model
.MODEL          DWELL2         D                LEVEL=1
+IS=7.6e-12     ISW=0.0        N=1.0                            M=2.9e-01
+VJ=5.3e-01     TT=0.0         CJO=7.4e-10      MJSW=0.0        FC=0.0     
+EG=1.1         XTI=1.5        AF=1.0           KF=0.0   

